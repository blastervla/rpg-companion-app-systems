base string name(name = "Name") = "";

base string source(name = "Source") = "";

base string level(name = "Level") = "spell_level_0";

base string school(name = "School") = "spell_school_abjuration";

base bool ritual(name = "Ritual") = false;

base string casting_time(name = "Casting time") = "";

calc string display_casting_time(name = "Display casting time") =
  if ritual then concat([casting_time, " or ritual"]) else casting_time;

base string range(name = "Range") = "";

base string duration(name = "Duration") = "";

base bool verbal(name = "Verbal") = false;

base bool somatic(name = "Somatic") = false;

base bool material(name = "Material") = false;

base string components(name = "Components") = "";

base string description(name = "Description") = "";

base string higher_level_description(name = "Higher level description") = "";

base string[] classes(name = "Classes") = [];

base resource<levelled_damage_dice>[] dice(name = "Dice") = [];

base resource<effect>[] effects(name = "Effects") = [];

base bool prepared(name = "Prepared") = false;

base integer cast_level(name = "Cast level") = 1;

calc bool is_cantrip(name = "Is cantrip") = level == "spell_level_0";

calc integer num_level(name = "Numerical level") =
  toNum(replace(string = level, occurrence = "spell_level_", replace = ""));

calc bool has_higher_level_description(name = "Has higher level description") =
  length(higher_level_description) > 0;

calc string display_type(name = "Type") =
  concat(
    [
      enumeratedName(enumerated_type = "spell_level", id = level),
      " ",
      enumeratedName(enumerated_type = "spell_school", id = school),
    ],
  );

calc string string_components(name = "Stringified components VSM") =
  joinToString(
    flatAppend(
      [
        (if true == verbal then ["V"] else []),
        (if true == somatic then ["S"] else []),
        (if true == material then ["M"] else []),
      ],
    ),
    separator = ", ",
  );

calc bool has_components(name = "Has components VSM") =
  verbal || somatic || material;

calc string display_requirements(name = "Requirements") =
  concat(
    [
      casting_time,
      (if has_components then concat([" (", string_components, ")"]) else ""),
    ],
  );

calc string full_components(name = "Full components") =
  if material
    then concat([string_components, " (", components, ")"])
    else string_components;

calc bool character_spell_slots_defined(name = "Character and spell slots defined") =
  defined(stat = "$character")
    && defined(stat = "$character.spell_slots_1")
    && defined(stat = "$character.spell_slots_2")
    && defined(stat = "$character.spell_slots_3")
    && defined(stat = "$character.spell_slots_4")
    && defined(stat = "$character.spell_slots_5")
    && defined(stat = "$character.spell_slots_6")
    && defined(stat = "$character.spell_slots_7")
    && defined(stat = "$character.spell_slots_8")
    && defined(stat = "$character.spell_slots_9");

calc string prepare_button_text(name = "Prepare button text") =
  if prepared then "UNPREPARE" else "PREPARE";

calc integer[] available_slots_for_use(name = "Available slots for use") =
  if character_spell_slots_defined
    then filter(
        [1, 2, 3, 4, 5, 6, 7, 8, 9],
        filter = ($filterValue) => ($filterValue >= num_level
          && metaStat(meta_stat = concat(["$character.spell_slots_", $filterValue])) > 0),
      )
    else [1];

calc string selected_spell_slots_stat_name(name = "Selected spell slots stat name") =
  concat(
    [
      "$character.spell_slots_",
      ($view?.spell_display_cast_composite_ticker? ?? "0"),
    ],
  );

calc integer available_selected_slots(name = "Available selected spell slots") =
  if character_spell_slots_defined
    && selected_spell_slots_stat_name != "$character.spell_slots_0"
    then metaStat(meta_stat = selected_spell_slots_stat_name)
    else 0;

calc bool should_select_spell_slot_to_cast(name = "Should select a spell slot to cast") =
  defined(stat = "$character") && !is_cantrip && !isEmpty(available_slots_for_use);

calc bool character_is_not_wearing_inproficient_armor(name = "Character is not currently wear a piece of armor its not proficient with") =
  $character?.is_not_wearing_inproficient_armor ?? true;

calc bool has_required_spell_slots_to_cast(name = "Has required spell slots to cast") =
  is_cantrip || !isEmpty(available_slots_for_use);

calc bool can_cast(name = "Can cast spell") =
  character_is_not_wearing_inproficient_armor && has_required_spell_slots_to_cast;
